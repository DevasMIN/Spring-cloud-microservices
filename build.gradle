plugins {
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    id 'org.liquibase.gradle' version '2.2.0'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.4.0'
}

allprojects {
    group = 'com.example'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    apply plugin: 'jacoco'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.google.cloud.tools.jib'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.11"
        reportsDirectory = layout.buildDirectory.dir('reports/jacoco')
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.40
                }
            }
        }
    }
}

// Конфигурация Liquibase
liquibase {
    activities {
        main {
            changeLogFile 'init-scripts/liquibase/db.changelog-master.xml'
            url 'jdbc:postgresql://localhost:5432/postgres'
            username 'postgres'
            password 'postgres'
        }
    }
}
